= Going Deeper: Nested Structures
++++
<style>
.sect2 table .listingblock code.pyret { font-size: 9pt; }
</style>
++++
@description{Students refactor code from a simple animation to include structures within structures, and see how to use nested structures in their own games and animations to manage complexity.}

[@lesson-intro-table]
|===
@lesson-prereqs{re-build-your-own-animation, re-refactoring}

| Lesson Goals
|
@objectives
@objective{manage-complexity}

| Materials
|[.materials-links]
@material-links

|===

== Nested Structures: Managing Complexity

=== Overview
Students are introduced to the need for nested data structures, as a way of managing complexity.

=== Launch
Now that you know all about data structures, you’re able to use them to make video games and animations from scratch, including games that are much more complex than those you worked on in Bootstrap:Algebra.

However, as you add more things to your game, you quickly end up with a large number of elements in your data structure. (If you have multiple characters in your game, each with their own position, speed, costume, etc. that all change, your structure can become quite long and unwieldy.)

@slidebreak

Making changes to your structure can get extremely complex, requiring lots of changes to your `next-tick` and `next-state-key` functions. One way to manage this complexity is to use _nested  structures_.

For example, if each of our 4 game characters have their own x and y coordinates, we could make one `Position` structure to use for each character. Then, instead of our game structure containing 8 numbers, it contains 4 `Position` structures.

@slidebreak

Let’s start out with a small animation to explore the benefits of nested structures. Open the @starter-file{pinwheels1} in Pyret, and click "Run". We see four colorful pinwheels spinning in the breeze.

```
# A PinwheelState is the angle of rotation for 4 pinwheels
data PinwheelState:
  | pinwheel(
      p1a :: Number,
      p2a :: Number,
      p3a :: Number,
      p4a :: Number)
end
STARTING-PINWHEELS = pinwheel(60, 3, 25, 70)
```

The only things that change in this animation are the angles of rotation for each of the 4 pinwheels.

@slidebreak

As usual, we have a `next-state-tick` function to handle updating the state of the animation, and a `draw-state` function to draw the animation.

We also have two @vocab{helper functions} to do some of the work for these main functions:

- `update-pinwheel`, which increases the angle for an individual pinwheel
- `draw-pinwheel`, which rotates the pinwheel image by the given angle.

Our `next-state-tick` function just calls `update-pinwheel` for each of the wheels. *Most of the work happens in these helper functions!*

@slidebreak

*Suppose we wanted each of the pinwheels to spin at a different speed.* We already know that any changeable part of the animation will need to be added to the structure, so means having to add 4 new fields to the `PinwheelState` structure.

=== Investigate
@lesson-instruction{
Print out the following @handout{non-nested-pinwheels.adoc, code screenshot} from the pinwheels file and underline or highlight each spot in the code you would need to change in order to add a speed to each pinwheel. Once you’ve identified which sections of the code will need to change, edit the program on the computer so that each pinwheel spins at a different speed.
}

@slidebreak

Now we have a nice animation of pinwheels spinning at different speeds, but this could have been a lot easier if we had started off by making each individual pinwheel _its own structure_.

@lesson-instruction{
Open the @starter-file{pinwheels2} on your computer and take a look at the code. What differences do you see between this starter file and the first?
}

@slidebreak

This time, the `PinwheelState` data structure contains four _structures_, instead Numbers. The angle of rotation is now contained inside the `Pinwheel` structure:

```
# A Pinwheel is an angle of rotation
data Pinwheel:
  | pw(angle :: Number)
end
# A PinwheelState is 4 Pinwheels
data PinwheelState:
  | pinwheels(
      p1 :: Pinwheel,
      p2 :: Pinwheel,
      p3 :: Pinwheel,
      p4 :: Pinwheel)
end
STARTING-PINWHEELS = pinwheels(pw(60), pw(3), pw(25), pw(70))
```

@slidebreak
@ifslide{
```
# A Pinwheel is an angle of rotation
data Pinwheel:
  | pw(angle :: Number)
end

# A PinwheelState is 4 Pinwheels
data PinwheelState:
  | pinwheels(
      p1 :: Pinwheel,
      p2 :: Pinwheel,
      p3 :: Pinwheel,
      p4 :: Pinwheel)
end

STARTING-PINWHEELS = pinwheels(pw(60), pw(3), pw(25), pw(70))
```
}

@QandA{
@Q{How would you get `p1` out of the `STARTING-PINWHEELS` instance?}
@A{`STARTING-PINWHEELS.p1`}
@Q{What type of data will you get back?}
@A{`pw(60)`}
@Q{How would you get `p1` out of the `STARTING-PINWHEELS` instance?}
@A{`STARTING-PINWHEELS.p1.angle`}
}

@slidebreak

Another change between the non-nested and nested versions of the code is that in the nested version, our @vocab{helper functions} `update-pinwheel` and `draw-pinwheel` now take in a `Pinwheel` data structure, as opposed to just a Number. The animation still works and looks the same on the outside, and the code hasn’t changed too drastically.


@slidebreak

Let's make each pinwheel spin at a different speed.

@lesson-instruction{
- On @handout{nested-pinwheels1.adoc}, underline or highlight _each spot in the code you would need to change_ in order to change each pinwheel’s speed independently.
- Edit the nested version of the program on the computer.
}

@teacher{Point out the differences in underlining between the two code screenshots. Note that when students finish this activity, both of the animations will look the same- but one program will have been much more straightforward to modify! We wrote a bit more code at the beginning to set up the nested structures, but that paid off later by giving us more flexibility to change the behavior of the pinwheels.}

@slidebreak

@ifnotslide{@center{@image{images/adding-speed.png, 800}}}

Just by looking at the differences on paper, we can see the difference in complexity of changing our animations. In order to make each pinwheel spin at a different speed, much more of the non-nested program will need to change, as opposed to the nested version where only the `Pinwheel` structure, `STARTING-PINWHEELS` instance, and the `update-pinwheel` function need to be edited.

@slidebreak

What if we wanted to add a breeze to our animation, and make the pinwheels move left across the screen? Let’s assume that each pinwheel moves at the same speed, but each of their x-coordinates will need to change.

@lesson-instruction{
Compare @handout{non-nested-pinwheels.adoc, Non-Nested Pinwheels} and @handout{nested-pinwheels2.adoc, Nested Pinwheels 2}, underlining or highlighting the places in the code you would need to edit in order to change the x-coordinates of each pinwheel. Do this for both the nested and non-nested versions of the animation.
}

@teacher{
@opt{For practice, have students make this change in both programs on the computer. Have them pay special attention to their helper functions- will they be able to use the existing update-pinwheel in the non-nested version of the animation?}
}
@scrub{

@slidebreak

@ifnotslide{@center{@image{images/adding-x-coordinates.png, 800}}}

As before, we end up underlining, and needing to change _much_ more of the code in the non-nested version of the animation. We also may realize something important about the non-nested code: if both a pinwheel’s angle of rotation _and_ its x-coordinate are changing, we’re no longer able to use our `update-pinwheel` @vocab{helper function}. Previously, this function consumed an angle and speed, and added these numbers together to produce the new angle. However, since functions can only return one thing at a time, we can’t use this function to produce the updated angle and updated x-coordinate. Instead, the work of decreasing the x-coordinate must be done inside `next-state-tick`. Writing that code is nothing new, but wouldn’t it be nice to leave `next-state-tick` alone, and update each pinwheel individually inside the @vocab{helper function}?

For reference, the complete code for the nested and non-nested versions of the pinwheels file including speed can be found here:

Non-nested Pinwheels+Speed
Nested Pinwheels+Speed
}

=== Synthesize

Compare the updating functions for the non-nested version of the code:

[cols="1a,1a", frame="none",options="header"]
|===
| Not-Nested
| Nested
|
```
# update-pinwheel :: Number, Number -> Number
fun update-pinwheel(angle, speed):
  angle + speed
end

# next-state-tick :: PinwheelState -> PinwheelState
fun next-state-tick(ps):
  pinwheel(
    update-pinwheel(ps.p1a, ps.p1speed),
    ps.p1speed,
    ps.p1x - 5,
    update-pinwheel(ps.p2a, ps.p2speed),
    ps.p2speed,
    ps.p2x - 5,
    update-pinwheel(ps.p3a, ps.p3speed),
    ps.p3speed,
    ps.p3x - 5,
    update-pinwheel(ps.p4a, ps.p4speed),
    ps.p4speed,
    ps.p4x - 5)
end
```
|
```
# update-pinwheel :: Pinwheel -> Pinwheel
fun update-pinwheel(p):
  pw(p.angle + p.speed, p.speed, p.x - 5)
end

# next-state-tick :: PinwheelState -> PinwheelState
fun next-state-tick(ps):
  pinwheels(
    update-pinwheel(ps.p1),
    update-pinwheel(ps.p2),
    update-pinwheel(ps.p3),
    update-pinwheel(ps.p4))
end
```
|===

The nested version is shorter, and it’s also more _readable_. If each pinwheel moves the same way, we can use one @vocab{helper function} on all of them, each time consuming a pinwheel and producing the updated pinwheel. The only function that needs to change is the one that works with `Pinwheel` structures (`update-pinwheel`).

The `next-state-tick` function doesn't need to change _at all_. This offers you lots more flexibility when making changes to your code, or adding things to a program.

@slidebreak

How do nested structures work in a complex video game? Here’s the original data block and some sample instances from a video game in which a player tries to catch the target, and avoid a danger:

```
# A GameState is a Player's x and y-coordinate, danger's x and y
# coordinate and speed, and target's x and y coordinate and speed
data GameState:
    game(
      playerx :: Number, playery :: Number,
      dangerx :: Number, dangery :: Number,
      dangerspeed :: Number,
      targetx :: Number, targety :: Number,
      targetspeed :: Number,
      score :: Number)
end
# Some sample GameStates
START = game(320, 100, 600, 75, 5, 1500, 250, 10, 0)
PLAY  = game(320, 100, 600, 75, 5, 300, 250, 20, 0)
```
@slidebreak

To clean up the `GameState` structure and allow more flexibility in our code, we defined a new structure to represent a `Character`, which contains a single set of x and y-coordinates:

```
# A Character is an x and y-coordinate
data Character:
    char(x :: Number, y :: Number)
end
data GameState:
    game(
      player :: Character,
      danger :: Character, dangerspeed :: Number,
      target :: Character, targetspeed :: Number,
      score :: Number)
end
# Some sample GameStates
START = game(char(320, 100), char(600, 75), 5, char(1500, 250), 10, 0)
PLAY  = game(char(320, 100), char(600, 75), 5, char(300, 250), 20, 0)
```

@slidebreak

@QandA{
For the nested structures version of Ninja Cat:

@Q{How would you get the _player’s x-coordinate_ out of START?}
@Q{What about the danger’s y-coordinate?}
@Q{How would you get the target’s speed out of PLAY?}
@Q{Finally, what do you notice about these two versions of the Ninja Cat data? Which do you prefer, and why?}
}

@teacher{Have students discuss the pros and cons of writing a game using nested or non-nested structures.}

@slidebreak

Now take a look at YOUR video games.

- If you were to re-write your program using nested structures, what would it look like?
- Do you have multiple characters in your game with their own x, y, and speed?
- Do you have any opportunities to use @vocab{helper functions} to move characters in the same way?

@lesson-instruction{
For practice, rewrite the data block and sample instances for your video game using nested structures.
}

@teacher{
@opt{If you like, have students completely refactor their entire game code to make use of nested structures and helper functions.}
}
